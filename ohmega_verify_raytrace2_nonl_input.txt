ohmega
bidirectional
run /bin/umodem raytrace_2_nonl.2d __END_RAYTRACE_2D__
,..|.....................................................................................,,...|.....................................................,,...............................................,
:X | *=================*   *=========================*                                   ::L  | *=================*+-+                              ::B *=============*  *==========================*:
:  +>!send[(W,S),(W,E)]!+  !send[(Inl(),S),(Inl(),E)]!+                                  :--+ +>!send[(W,S),(W,E)]!+ v                              :-->!case W of S,E!->!send[(Inl(Inl(),Inr()),E)]!-
:    *=================*|  *=========================*|                                  :: v   *=================* *======================*        ::  *=============*  *==========================*:
:          | *=======* +#--+       |                  |                                  ::*=============*|       +>!send[(Inl(N,Inr()),E)]!---------:   |       *=====*                             :
--+        +>!split W!-+|  v       |                  |                                  ::!case N of S,E!#-------+ *======================*        ::   v     +>!use B!+                            :
: v          *=======*+-+ *=====*  |     +------------+                                  ::*=============*|                          *=============*::*=======*| *=====*v                            :
:*=================*|+#-->!use Z!--#--+  v                                               :: |             v    *=================* +>!case W of S,E!-:!split N!+  *======================*           :
:!send[(N,E),(N,S)]!#+|   *=====* ++  | *=====*  *=================*                     :: v         *=====*+>!send[(W,E),(W,S)]!-+ *=============*::*=======* +>!send[(Inl(Inl(),N),E)]!------------
:*=================*v | +-+     +-+   +>!use L!->!send[(W,S),(W,E)]!+                    ::*=======*+>!use L!+ *=================*+---+             ::       +--+ *======================*           :
: |            *=====*| | v     |+-+    *=====*  *=================*|                    ::!split N!+ *=====*                +----#----+            :,...............................................,
: |            !use T!#-+*=====*|| v                          |     v                    ::*=======*                              v    v            :,..|............................................,
: v            *=====*|+>!use Z!#+*=====*  *=================*| *=====================*  :: |                              *=======*  *===========* ::R | *=================*  *=================*   :
:*=================*  || *=====*+>!use R!->!send[(W,S),(W,E)]!#>!send[(N,S),((N,W),E)]!+ :: |                              !split N!->!send[(N,S)]! :---#>!send[(W,S),(W,E)]!->!send[(W,S),(W,E)]!+  :
:!send[(N,E),(N,S)]!--#+          *=====*  *=================*| *=====================*| :: |                              *=======*  *===========* ::  v *=================*  *=================*|  :
:*=================*+-#---------------------#-----------------#-+|                     | :: |                              ++          |            ::*=============*|          |                 |  :
:                 +-+ v           +---------#-----------------+ +#-----+               | :: |                              v           v            ::!case N of S,E!#+         v                 |  :
:                   *=======*+----#-+       v                    |     v               v :: |                         *=====*  *==================* ::*=============*|+-+ *======================*|  :
:                   !split N!+    v |  *=====*    *=============*| *===========*  *=====*:: +------------------------>!use l!->!send[(Inl(W,N),E)]!--: | *=======*  ++  +>!send[(Inl(N,Inr()),E)]!#---
:                   *=======**=====*+->!use Q!+ +>!case W of E,S!#>!send[(N,E)]!->!use X!-:                           *=====*  *==================* :: +>!split W!--#----+*======================*|  :
:                          +>!use Q!-+ *=====*v | *=============*v *===========*  *=====*:,.........................................................,:   *=======*  v    v                        v  :
:                            *=====* |   *=====*|            | *=====*                   :,.........|...........................................,    :       | *=====*  *=====*  *==================*:
:                                    +-->!use D!+            +>!use H!--------------------:E        | *=================*                       :    :       +>!use r!->!use R!->!send[(Inl(N,W),E)]!-
:                                        *=====*               *=====*                   :---+      +>!send[(W,E),(W,S)]!+   *===============*  :    :         *=====*  *=====*  *==================*:
,........................................................................................,:  v        *=================*v +>!send[(Inr(),E)]!---    ,...............................................,
,..|...............................................,,.................|..................,:*=============*| *=============*| *===============*  :    ,....|..........................................,
:Q |          *=================*                  ::M *=============*| *===============*::!case N of E,S!#>!case N of E,S!+  *===============* :    :l   | *=================*                      :
---#--------->!send[(W,E),(W,S)]!+                 :-->!case W of E,S!#>!send[(Inl(),E)]!-:*=============*v *=============* +>!send[(Inl(),E)]!--    ---+ +>!send[(W,S),(W,E)]!------+               :
:  v          *=================*v                 ::  *=============*v *===============*:: |        *=================* +--+ *===============* :    :  v   *=================*      |               :
:*=============*         |      *===========*      ::   | *=================*            :: |        !send[(N,E),(N,S)]!+    *===============*  :    :*=======*| *=======*  *=======*| *=======*     :
:!case N of S,E!---------#----->!send[(N,E)]!-------:   | !send[(N,S),(N,E)]!+           :: v        *=================*v  +>!send[(Inl(),E)]!---    :!split N!#>!split W!->!split W!#>!split W!-+   :
:*=============*         v      *===========*      ::   v *=================*v           ::*=============*| *=============*| *===============*  :    :*=======*| *=======*  *=======*| *=======* |   :
:++*=================*  *=============*            ::*=============*| *===========*      ::!case N of E,S!#>!case N of E,S!+   *===============*:    :       +-#----#----+    |     ++   |       |   :
:+>!send[(W,S),(W,E)]!->!case N of S,E!+           ::!case N of S,E!#>!send[(N,E)]!-------:*=============*| *=============*  +>!send[(Inr(),E)]!-    :+--------+    |  +-+    v   +-+    v       |   :
:  *=================*  *=============*v           ::*=============*v *===========*      :: |             | ++*=============*++*===============*:    :|             v  | *=====*  | *=====*      |   :
: +-+                    |        *===============*::++*=============*  *===============*:: |             | +>!case W of E,S!-+*===============*:    :|*=============* +>!use M!+ +>!use M!+     |   :
: | *=======*    +-------+        !send[(Inl(),E)]!-:+>!case N of E,S!->!send[(Inl(),E)]!-: |             |   *=============*+>!send[(Inl(),E)]!-    :|!case N of E,S!+  *=====*v   *=====*|     v   :
: +>!split W!----#--------------+ *===============*::  *=============*  *===============*:: |             v                +-+ *===============*:    :|*=============*| *===========*     ++  *=====*:
:   *=======*    v              |                  ::   | *===================*          ::++*=============*                   *===============*:    :|  |            +>!send[(N,S)]!     v +>!use A!-
:+---+    *=======*        +--+ +------------+     ::   +>!send[(Inr Inl(),E)]!-----------:+>!case N of E,S!------------------>!send[(Inl(),E)]!-    :|  v              *===========**=====*| *=====*:
:|        !split N!--------+  v              v     ::     *===================*          ::  *=============*                   *===============*:    :| *===========*              +>!use A!+        :
:|        *=======*          *===========*  *=====*:,....................................,:               +--------------------------------------    :+>!send[(W,E)]!+               *=====*         :
:|      +--+*=============*+>!send[(N,E)]!->!use Q!-,.....|...........................,   ,.....................................................,    :  *===========*+--------------------------------
:|      v +>!case W of S,E!+ *===========*  *=====*::H    v            *=======*      :   ,.....|...........................................,        ,...............................................,
:| *=====*| *=============*  *===============*     :: *=============*+>!split W!+     :   :main v               *=====*  *===============*  :        ,....|..........................................,
:+>!use E!+              +-->!send[(Inl(),E)]!------->!case N of E,S!+ *=======*v     :   :*=================*+>!use B!->!send[((W,W),E)]!+ :        :r   | *=================*                      :
:  *=====*                   *===============*     :: *=============*  ++*===========*:   :!send[(N,E),(N,S)]!+ *=====*  *===============*v :        ---+ +>!send[(W,S),(W,E)]!------+               :
,..................................................,:  |               +>!send[(W,E)]!-   :*=================*                       *=====*:        :  v   *=================*      |               :
,..|.............................................,  :  |                 *===========*:   :                 +----------------------->!use X!-        :*=======*| *=======*  *=======*| *=======*     :
:Z | *=============*  *===============*          :  :  +-------------------------------   :                                          *=====*:        :!split N!#>!split W!->!split W!#>!split W!-+   :
:  +>!case W of S,E!->!send[(Inr(),E)]!-----------  ,.................................,   ,.................................................,        :*=======*| *=======*  *=======*| *=======* |   :
:    *=============*  *===============*          :  ,..|.............................,    ,.......|..................................,               :       +-#----#----+    |     ++   |       |   :
:     |                *=============*           :  :T v              *=======*      :    :A      | *=================*              :               :+--------+    |  +-+    v   +-+    v       |   :
:     v              +>!case W of S,E!+          :  :*=============*+>!split W!+     :    ---+    +>!send[(W,E),(W,S)]!+             :               :|             v  | *=====*  | *=====*      |   :
: *=================*| *=============*v          :  :!case N of E,S!+ *=======*v     :    :  v      *=================*v             :               :|*=============* +>!use M!+ +>!use M!+     |   :
->!send[(N,S),(W,E)]!+  |       *===============*:  :*=============*  ++*===========*:    :*=============*| *===========*            :               :|!case N of S,E!+  *=====*v   *=====*|     v   :
: *=================*   |       !send[(Inr(),E)]!-  : |               +>!send[(N,E)]!-    :!case N of E,S!#>!send[(N,E)]!-------------               :|*=============*| *===========*     ++  *=====*:
:    |              +---#------+*===============*:  : |                 *===========*:    :*=============*| *===========*            :               :|  |            +>!send[(N,S)]!     v +>!use A!-
:    v    +---------+   v      ++                :  : +-------------------------------    :   |           |                          :               :|  v              *===========**=====*| *=====*:
:*=======*|         *=======*   v                :  ,................................,    :   v           |                          :               :| *===========*              +>!use A!+        :
:!split N!+         !split N!+ *=====*           :  ,...................|...........,     :*=============*| *=======================*:               :+>!send[(W,E)]!+               *=====*         :
:*=======*        ++*=======*+>!use Z!+          :  :D                  v           :     :!case N of S,E!#>!send[(Inr Inr Inl(),E)]!-               :  *===========*+--------------------------------
:++               v+-+         *=====*v          :  : *=============*  *===========*:     :*=============*v *=======================*:               ,...............................................,
:| *===============*   *==================*      :  ->!case W of S,E!->!send[(N,E)]!-     :++*=============*  *===================*  :
:+>!send[((W,N),E)]!-->!send[(Inl(W,N),E)]!-------  : *=============*  *===========*:     :+>!case N of E,S!->!send[(Inr Inl(),E)]!---
:  *===============*   *==================*      :  :            | *===============*:     :  *=============*  *===================*  :
,................................................,  :            +>!send[(Inl(),E)]!-     :        | *=======================*       :
                                                    :              *===============*:     :        +>!send[(Inr Inr Inl(),E)]!--------
                                                    ,...............................,     :          *=======================*       :
                                                                                          ,..........................................,
__END_RAYTRACE_2D__
verify raytrace raytrace_2_nonl.2d
